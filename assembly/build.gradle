apply plugin: 'distribution'

configurations {
    shared

    all*.exclude group: 'commons-logging', module: 'commons-logging'
}

dependencies {
    shared 'nl.eveoh.mytimetable:mytimetable-api-client:1.1.1'
    shared 'org.slf4j:slf4j-log4j12:1.7.5'
}

distributions {
    main {
        baseName = 'sakai-mytimetable-tomcat-overlay'

        contents {
            into('shared/lib') {
                from parent.project(':sakai-mytimetable-api').tasks['jar'].archivePath

                // Include the MyTimetable Client API
                from configurations.shared.files
            }

            into('components/sakai-mytimetable-pack') {
                // unzip the WAR archive
                from zipTree(project(':sakai-mytimetable-pack').tasks['war'].archivePath)
            }

            into('webapps/sakai-mytimetable-tool') {
                // unzip the WAR archive
                from zipTree(project(':sakai-mytimetable-tool').tasks['war'].archivePath)
            }
        }
    }
}

distZip.dependsOn ':sakai-mytimetable-pack:war'
distZip.dependsOn ':sakai-mytimetable-tool:war'

distTar.dependsOn ':sakai-mytimetable-pack:war'
distTar.dependsOn ':sakai-mytimetable-tool:war'

distTar {
    compression = Compression.GZIP
}